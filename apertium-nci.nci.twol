Alphabet
 
 A B C D E F G H I J K L M N O P Q R S T U W Y Z
 a b c d e f g h i j k l m n o p q r s t u w y z
 ā ē ī ō ū Ā Ē Ī Ō Ū 
 %{o%}:o  
 %{i%}:i %{i%}:0
 %{h%}:h %{h%}:u
 %{u%}:u %{u%}:h
 %{k%}:c %{k%}:q
 %{w%}:u %{w%}:0
 %{n%}:n %{n%}:m
 %{c%}:h %{c%}:0
  %>:%>  ;

Sets

Vow   = A E I O U Ā Ē Ī Ō Ū
        a e i o u ā ē ī ō ū ;

LongVow =  Ā Ē Ī Ō Ū
           ā ē ī ō ū ;

Cns   = b c d f g h j k l m n p q r s t w y z ;

Rules 

"Epenthesis of {i} before consonants"
%{i%}:i <=> _ [ %>: ]+ :Cns ;
    except 
      %{k%}: %{w%}: _ ;

!@ t {i}:i > > {k}:c {w}:0 {i}:0 > c h ī h u a

"Nasal assimilation"
n:0 <=> _ %>: m ; 

"Nasal elision"
n:0 <=> _ %>: h u a n ;

"Vowel deletion"
i:0 <=> %{o%}: %>: _ Cns ; 

"Possessive nasal assimilation"
m:n <=> i _ %>: [ c | t | n | s | y ] ;

! TODO:
!define DeleteIAfterReflexiveO i -> 0 || o "^" SuffixFlags _ Cons Cons ;
!define PossVEnding a -> a u h, o -> o u h, i -> i u h, e -> e u h || _ "^" ("@D.PL@") PossFlags .#. ;
!!xi                             W @D.PL@@R.poss@
!define WrewritePt1 W -> u h  || Vowels* _ "^" NominalFlags* .#. ;
!define WrewritePt2 W -> h u i || _ NominalFlags* "^" t l .#. ;

"Metathesis of -{u}{h} for {h}"
%{u%}:h <=>  _ %{h%}: %>: i t l ;
             _ %{h%}: Vow %>: z ; 

!@ x i {u}:h {h}:u >:> i t l
!@ x i {u}:h {h}:u >:> i t l 
!@ n {o}:o >:> x i {u}:u {h}:h 
!@ ō > n {i}:i > > {k}:c {w}:0 {i}:0 > c h ī {h}:u {u}:h > {c}:0
!@ n {i}:i > > {k}:c {w}:0 {i}:0 > c h ī {u}:h {h}:u a > z

"Metathesis of -{u}{h} for {u}"
%{h%}:u <=> %{u%}:h _ ; 

"Orthographic rule for /k/, -c-/-qu(i)- (1)"
%{k%}:c <=> _ %{w%}: %{i%}: %>: Cns ;

!@ > m i {k}:c {w}:0 {i}:0 > q u i

!@ n {i}:i > > m i {k}:q {w}:u {i}:i > z

"Orthographic rule for /k/, -c-/-qu(i)- (2)"
%{w%}:0 <=> %{k%}:c _ ;

"Orthographic rule for /k/, -c-/-qu(i)- (3)"
%{i%}:0 <=> %{k%}:c %{w%}:0 _ ;
                    %{w%}:u _ %>: i ; 

!@ {k}:q {w}:u {i}:0 > i t t a

"Assimilation of n-"
%{n%}:m <=> _ %>: [ Vow | m | p ]  ;

"Preterite singular -c/-h/-0"
%{c%}:h <=> [ :Vow - :LongVow ] %>: _ ; 


!@ ō > > {k}:c {w}:0 {i}:0 > t l ā l ī > {c}:0
