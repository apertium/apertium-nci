Alphabet
 
 A B C D E F G H I J K L M N O P R S T U W Y Z
 a b c d e f g h i j k l m n o p r s t u w y z
 %{o%}:o  
 %{i%}:i %{i%}:0
 %{h%}:h %{h%}:u
 %{u%}:u %{u%}:h
 %{k%}:c %{k%}:q
 %{w%}:u %{w%}:0
 %{n%}:n %{n%}:m
  %>:%>  ;

Sets

Vow   = A E I O U 
        a e i o u ;

Cns   = b c d f g h j k l m n p r s t w y z ;

Rules 

"Epenthesis of {i} before consonants"
%{i%}:i <=> _ [ %>: ]+ :Cns ;

"Nasal assimilation"
n:0 <=> _ %>: m ; 

"Nasal elision"
n:0 <=> _ %>: h u a n ;

"Vowel deletion"
i:0 <=> %{o%}: %>: _ Cns ; 

"Possessive nasal assimilation"
m:n <=> i _ %>: [ c | t | n | s | y ] ;

! TODO:
!define DeleteIAfterReflexiveO i -> 0 || o "^" SuffixFlags _ Cons Cons ;
!define PossVEnding a -> a u h, o -> o u h, i -> i u h, e -> e u h || _ "^" ("@D.PL@") PossFlags .#. ;
!!xi                             W @D.PL@@R.poss@
!define WrewritePt1 W -> u h  || Vowels* _ "^" NominalFlags* .#. ;
!define WrewritePt2 W -> h u i || _ NominalFlags* "^" t l .#. ;

"Metathesis of -{h}{u} for {h}"
%{u%}:h <=>  _ %{h%}: %>: i t l ;

!@ x i {u}:h {h}:u >:> i t l
!@ x i {u}:h {h}:u >:> i t l 
!@ n {o}:o >:> x i {u}:u {h}:h 

"Metathesis of -{h}{u} for {u}"
%{h%}:u <=> %{u%}:h _ ; 

"Orthographic rule for /k/, -c-/-qu(i)- (1)"
%{k%}:c <=> _ %{w%}: %{i%}: %>: Cns ;

"Orthographic rule for /k/, -c-/-qu(i)- (2)"
%{w%}:0 <=> %{k%}:c _ ;

"Orthographic rule for /k/, -c-/-qu(i)- (2)"
%{i%}:0 <=> %{k%}:c %{w%}:0 _ ;

"Assimilation of n-"
%{n%}:m <=> _ %>: [ Vow | m | p ]  ;
